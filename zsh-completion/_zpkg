#compdef zpkg

_commands=(
    '-h:print the help infomation of zpkg command.'
    '--help:print the help infomation of zpkg command.'
    '-V:print the version of zpkg.'
    '--version:print the version of zpkg.'
    'search:search packages can be installed.'
    'ls:list the available, installed and outdated packages.'
    'tree:list contents of a installed package directory in a tree-like format.'
    'info:print the basic infomation of packages.'
    'formula:view,edit the formula of a package.'
    'install:install packages.'
    'reinstall:reinstall packages.'
    'uninstall:uninstall packages.'
    'cleanup:cleanup the unused cache.'
    'update:update the formula repository.'
    'upgrade:upgrade the outdated packages.'
    'upgrade-self:upgrade self.'
    'fetch:download formula resources of a package to the cache.'
    'homepage:visit the homepage of a formula or the zpkg project.'
    'integrate:integrate zsh completions.'
    'depends:show the depends of a package.'
    'prefix:show the installation direcotory of a formula or the zpkg home.'
    'pack:pack a installed package.'
    'logs:print the logs of a installed package.'
    'get:get value of the given key of the given package.'
    'is:check something. easy to write script.'
    'gen-github-workflows:generate github workflows.'
)

_list=(
    'available:list the available packages.'
    'installed:list the installed packages.'
    'outdated:list the installed packages which can be upgraded.'
)

function _zpkg_available_packages() {
    local -a _available_packages
    _available_packages=($(zpkg list available -q))
    _describe 'available-packages' _available_packages
}

function _zpkg_installed_packages() {
    local -a _installed_packages
    _installed_packages=($(zpkg list installed))
    _describe 'installed-packages' _installed_packages
}

function _zpkg_outdated_packages() {
    local -a _outdated_packages
    _outdated_packages=($(zpkg list outdated))
    _describe 'outdated-packages' _outdated_packages
}

function _zpkg() {
    local context state state_descr line
    typeset -A opt_args

    _arguments \
        '1: :{_describe 'command' _commands}' \
        '*:: :->option'

    case $state in
        option)
            case $words[1] in
                ls)
                    _arguments \
                        ':list:{_describe 'list' _list}' \
                        '-q[quiet mode. do not report error log]'
                    ;;
                formula)
                    _arguments \
                        ':action:(create delete rename view edit list repo)' \
                        '*:: :->action'
                    case $state in
                        action)
                            case $words[1] in
                                delete|rename|view|edit)
                                    _arguments ':package-name:_zpkg_available_packages'
                                    ;;
                                repo)
                                    _arguments '1:action:(list add del)' '*:: :->repo-subcommand'
                                    case $state in
                                        repo-subcommand)
                                            case $words[1] in
                                                del) _arguments '1:repo-name:($(ipkg formula repo list | cut -d= -f1))' ;;
                                            esac
                                    esac
                                    ;;
                            esac
                    esac
                    ;;
                info)
                    _arguments '*:package-name:_zpkg_available_packages' ;;
                fetch|homepage|depends)
                    _arguments ':package-name:_zpkg_available_packages' ;;
                prefix|pack|tree)
                    _arguments ':package-name:_zpkg_installed_packages' ;;
                logs)
                    _arguments \
                        '1:package-name:_zpkg_installed_packages'
                    ;;
                get)
                    _arguments \
                        '1:package-name:_zpkg_available_packages' \
                        '2:key:(summary webpage version license src.git src.url src.sum dep.cmd dep.pkg patches cdefine ccflags xxflags ldflags sourced binsrcd bsystem)'
                    ;;
                install)
                    _arguments \
                        ':package-name:_zpkg_available_packages' \
                        '--jobs=-[set the number of jobs you can run in parallel with make]:jobs:(1 2 3 4 5 6 7 8 9)' \
                        '--dry-run[dry-run]' \
                        '--keep-working-dir[do not delete working-dir even if installed success]' \
                        '(--verbose -v)'{--verbose,-v}'[verbose output log]' \
                        '(--debug -d)'{--debug,-d}'[debug]' \
                        '(--xtrace -x)'{--xtrace,-x}'[trace execution of commands]'
                    ;;
                reinstall)
                    _arguments \
                        ':package-name:_zpkg_installed_packages' \
                        '--jobs=-[set the number of jobs you can run in parallel with make]:jobs:(1 2 3 4 5 6 7 8 9)' \
                        '(--verbose -v)'{--verbose,-v}'[verbose output log]' \
                        '(--debug -d)'{--debug,-d}'[debug]' \
                        '(--xtrace -x)'{--xtrace,-x}'[trace execution of commands]'
                    ;;
                upgrade)
                    _arguments \
                        ':package-name:_zpkg_outdated_packages' \
                        '--jobs=-[set the number of jobs you can run in parallel with make]:jobs:(1 2 3 4 5 6 7 8 9)' \
                        '(--verbose -v)'{--verbose,-v}'[verbose output log]' \
                        '(--debug -d)'{--debug,-d}'[debug]' \
                        '(--xtrace -x)'{--xtrace,-x}'[trace execution of commands]'
                    ;;
                uninstall)
                    _arguments '*:package-name:_zpkg_installed_packages' ;;
                is)
                    _arguments \
                        '1: :(available installed outdated)' \
                        '2:package-name:_zpkg_available_packages'
                    ;;
                integrate)
                    _arguments ':what:(zsh)'
                    ;;
                gen-github-workflows)
                    _arguments \
                        '--group-size=-[set the group size]:group-size:(50 40 30 20)' \
                        '--output-dir=-[set the output dir]:output-dir:{_files -/}'
                    ;;
                *);;
            esac
            ;;
        *);;
    esac
}

_zpkg "$@"
